/*
 * Copyright 2020 ZUP IT SERVICOS EM TECNOLOGIA E INOVACAO SA
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import br.com.zup.beagle.Dependencies

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.facebook.testing.screenshot'
apply from: "../screenshots.gradle"

android {
    compileSdkVersion Dependencies.Versions.compileSdk
    buildToolsVersion Dependencies.Versions.buildTools
    defaultConfig {
        applicationId Dependencies.ApplicationId.id

        minSdkVersion Dependencies.Versions.minSdk
        targetSdkVersion Dependencies.Versions.targetSdk

        versionCode Dependencies.Releases.versionCode
        versionName Dependencies.Releases.versionName

        testInstrumentationRunner "com.karumi.shot.ShotTestRunner"

        if (project.ext.has("snapshot")) {
            testApplicationId "br.com.zup.beagle.sample.test"
            testInstrumentationRunner "br.com.zup.beagle.sample.SampleTestRunner"
        } else {
            testApplicationId "br.com.zup.beagle.sample.cucumber.steps"
            testInstrumentationRunner "br.com.zup.beagle.sample.cucumber.steps.Instrumentation"
        }


        buildConfigField "String", "TEST_TAGS", "\"${getTestTags()}\""

    }

    sourceSets {
        androidTest {
            assets.srcDirs = ['src/androidTest/assets']
        }
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {

    implementation Dependencies.AndroidSupport.design
    implementation Dependencies.GeneralLibraries.kotlin

    debugImplementation project(Dependencies.Modules.preview)

    implementation project(Dependencies.Modules.core)
    kapt project(Dependencies.Modules.processor)

    // Unit Testing
    testImplementation Dependencies.TestLibraries.junit
    testImplementation Dependencies.TestLibraries.kotlinTest
    testImplementation Dependencies.TestLibraries.mockk

    // Integration Testing
    androidTestImplementation Dependencies.TestLibraries.testRunner
    androidTestImplementation Dependencies.TestLibraries.espressoCore
    androidTestImplementation Dependencies.TestLibraries.supportAnnotation
    androidTestImplementation Dependencies.TestLibraries.cucumberAndroid
    androidTestImplementation Dependencies.TestLibraries.cucumberPicocontainer
}

def getTestTags() {
    return project.hasProperty("tags") ? project.getProperties().get("tags") : ""
}


screenshots {
    multipleDevices true
}

task verifyScreenshot (type:Exec) {
    def recordedPath = "$rootProject.projectDir/sample/screenshots/API_21_GP_XXHDPI_1080x1920_x86_"
    def runPath = "$rootProject.projectDir/sample/build/screenshotsDebugAndroidTest"
    commandLine 'python3', 'segment.py', recordedPath, runPath
}
